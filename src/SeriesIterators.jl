module SeriesIterators

export iterm, icumsum
export bernoulli

using IterTools
using Base.Iterators

import Base: iterate, IteratorSize, length, size

"""
    iterm(g, ind)

Assume a sequence of terms a[k] can be generated by

    a[i0], b = g(i0)
    a[i], b =  g(i, b) for subsequent subsequent indices

The `iterm(g, ind)` iterates the sequence, when `ind` is the
iterator of the indices; first index is `i0`.
"""
iterm(gen, it) = TermIterator(gen, it)
struct TermIterator{F,I}
    generate::F
    indices::I
end
function iterate(t::TermIterator)
    vs = iterate(t.indices)
    k, s = @IterTools.ifsomething vs
    v = t.generate(k)
    v[1], (s, v)
end
function iterate(t::TermIterator, s)
    sx, p = s
    vs = iterate(t.indices, sx)
    k, s = @IterTools.ifsomething vs
    v = t.generate(k, p)
    v[1], (s, v)
end
IteratorSize(::Type{<:TermIterator{<:Any,I}}) where I = IteratorSize(I)
length(t::TermIterator) = length(t.indices)
size(t::TermIterator) = size(t.indices)

function gen_bernoulli(::Type{T}) where T<:Integer
    B = Vector{Rational{T}}(undef, 2)
    B[1] = 1
    B[2] = -1 // 2
    Z = zero(Rational{BigInt})
    function _bernoulli(n::Integer)
        b = B
        n <= 1 && return b[n+1]
        isodd(n) && return Z
        l = length(b)
        if l * 2 - 2 <= n
            ext = min(1_000_000 ÷ l^2, 40)
            m = max(n ÷ 2 + 2, l + ext)
            n0 = (length(b) - 1 ) * 2
            sizehint!(b, ((m + 80) >> 6) << 6)
            resize!(b, m)
            _bernoulli!(b, n0, (m - 1) * 2 - 1)
        end
        b[n÷2 + 2]
    end
    _bernoulli
end

function _bernoulli!(B::Vector{Rational{T}}, n0::Integer, n::Integer) where T
    for m = n0:2:n
        sum = -B[1] // (m + 1)
        sum -= B[2]
        mok = T((m * (m - 1)) ÷ 2)
        for k = 2:2:m-1
            sum -= B[k÷2 + 2] * mok // (m - k + 1)
            mok *= (m - k) // (k + 1)
            mok *= (m - k - 1) // (k + 2)
        end
        B[m÷2 + 2] = sum
    end
    nothing
end
"""
    bernoulli(n)

Return Bernoulli number `B_n`. `B_1 = -1/2`.
"""
const bernoulli = gen_bernoulli(BigInt)






end
